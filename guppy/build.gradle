apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply from: './jacoco.gradle'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'org.jetbrains.dokka'

group = 'com.github.johnsonandjohnson'
version = '1.0.1'

android {
    compileSdkVersion rootProject.ext.targetSdk
    buildFeatures {
        viewBinding true
    }
    defaultConfig {
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode 2
        versionName version
        buildConfigField "int", "databaseVersion", "2"
        buildConfigField 'int', 'VERSION_CODE', "${defaultConfig.versionCode}"
        buildConfigField 'String', 'VERSION_NAME', "\"${defaultConfig.versionName}\""
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            debuggable true
            testCoverageEnabled true
            resValue "string", "databaseName", "guppy_dev.db"
        }
        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "databaseName", "guppy_release.db"

        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.mockito:mockito-inline:3.6.28'
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"
    testImplementation "com.squareup.okhttp3:okhttp-tls:$okhttp_version"
    debugImplementation 'androidx.fragment:fragment-testing:1.2.5'

    androidTestImplementation "androidx.test:runner:$testrunner_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.recyclerview:recyclerview:$androidx_widgets"
    implementation "androidx.appcompat:appcompat:$androidx_appcompat"
    implementation "androidx.constraintlayout:constraintlayout:$androidx_constraint"

    //OkHTTP
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    //GSON
    implementation "com.google.code.gson:gson:$gson_version"
}


task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.sourceFiles
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set("javadoc")
    from "$buildDir/javadoc"
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

jacocoAndroidUnitTestReport {
    destination "${buildDir}/reports/jacoco/jacocoTestReportCoverage/"
}